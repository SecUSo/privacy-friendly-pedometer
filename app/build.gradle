apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

def pfaFile = rootProject.file('pfa.properties')

android {
    namespace 'org.secuso.privacyfriendlyactivitytracker'

    signingConfigs {
        if (pfaFile.exists()) {
            'pfa-key' {
                def pfa = new Properties()
                pfa.load(new FileInputStream(pfaFile))

                storePassword pfa['storePassword']
                keyAlias pfa['keyAlias']
                keyPassword pfa['keyPassword']
                storeFile file(pfa['storeFile'])
            }
        }
    }
    compileSdk 33

    defaultConfig {
        applicationId 'org.secuso.privacyfriendlyactivitytracker'
        minSdkVersion 17
        targetSdkVersion 33
        versionCode 15
        versionName "3.0.1"
    }

    applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "pfa-pedometer-${variant.name}-v${variant.versionName}.apk"
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            debuggable true
            if (pfaFile.exists()) {
                signingConfig signingConfigs.'pfa-key'
            }
        }
    }
    productFlavors {
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    buildToolsVersion '33.0.0'
}

repositories {
    maven { url "https://jitpack.io" }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation project(path: ':backup-api')
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.9.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.3.1'
    implementation 'com.github.PhilJay:MPAndroidChart:v3.0.0-beta1'

    def work_version = "2.8.1"
    implementation "androidx.work:work-runtime:$work_version"
    implementation "androidx.work:work-runtime-ktx:$work_version"
    androidTestImplementation "androidx.work:work-testing:$work_version"

    implementation "androidx.sqlite:sqlite-ktx:2.3.1"
    implementation 'androidx.sqlite:sqlite-framework:2.3.1'
}

